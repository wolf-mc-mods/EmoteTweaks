plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "me.shedaniel.unified-publishing"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "me.shedaniel:architectury-fabric:${rootProject.architectury_version}"

    modApi "io.github.kosmx.emotes:emotesMain:${rootProject.emotecraft}"
    modApi "dev.kosmx.player-anim:player-animation-lib-fabric:${rootProject.player_animation_lib}"
    modApi "de.maxhenkel.voicechat:voicechat-api:${rootProject.voicechat_api}"
    modApi "maven.modrinth:simple-voice-chat:fabric-${rootProject.voicechat}"
    modApi "maven.modrinth:emotecraft:${rootProject.emotecraft}+1.16.5-fabric"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "fabric"

    from rootProject.file("LICENSE.md")
}


unifiedPublishing {
    project {
        displayName = "[Fabric $rootProject.minecraft_version] v$project.version"
        releaseType = "release"
        gameVersions = ["1.16.5"]
        gameLoaders = ["fabric"]

        mainPublication remapJar

        relations {
            depends {
                curseforge = modrinth = "emotecraft"
            }
            depends {
                curseforge = modrinth = "simple-voice-chat"
            }
        }

        if (project.hasProperty("curse_api_key") || System.getenv("curse_api_key") != null) {
            curseforge {
                token = project.hasProperty("curse_api_key") ? project.property("curse_api_key") : System.getenv("curse_api_key")
                id = project.curseforge_id
                gameVersions.addAll "Java 8"
            }
        }

        if (project.hasProperty("modrinth_key") || System.getenv("modrinth_key") != null) {
            modrinth {
                token = project.hasProperty("modrinth_key") ? project.property("modrinth_key") : System.getenv("modrinth_key")
                id = project.modrinth_id
                version = "$project.version+$project.name"
            }
        }
    }
}


publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

rootProject.tasks.getByName("modPublish").dependsOn tasks.getByName("publishUnified")